IMPORTATION:
  # Import the time
  Time = time.des

INITIALIZER:
    from Tkinter import *
    from DWatchGUI import DWatchGUI
    
    root = Tk()
    root.title("A Title")
    g = DWatchGUI(root, eventhandler)
    
    eventhandler.start()
    root.mainloop()
    
    exit() # keep SVM from crashing at exit

STATECHART:
    Clock [CS][DS]
    Running [CS][DS]
        TimeUnit [CS][DS] [Time] [IMPORTED=1]
        Light [CS][DS]
            LightOff [DS]
            LightOn
            LightWait

        Mode [CS][DS]
            DisplayMode [DS]
                update [CS][DS]
                TimeEditWait
                
            TimeEdit
                Selection [CS] [DS]
                TimeEditEndWait [CS] [DS]
                FinishedWait [CS] [DS]
                    FinishedTimer

            ChronoMode
                Initial [DS]
                Started
                Stopped

# Light on and off

TRANSITION:
    S: Running.Light.LightOff
    N: Running.Light.LightOn
    E: lightOn
    O: print "DisplayMode.LightOff -> DisplayMode.LightOn"

TRANSITION:
    S: Running.Light.LightOn
    N: Running.Light.LightWait
    E: lightOff
    O: print "light on -> light wait"

TRANSITION:
    S: Running.Light.LightWait
    N: Running.Light.LightOff
    T: 2
    O: print "light wait -> light off"
       g.unsetIndiglo()

# switch between display and chrono mode

TRANSITION:
    S: Running.Mode.DisplayMode
    N: Running.Mode.ChronoMode
    E: changeMode
    O: print "DisplayMode -> ChronoMode"
       g.refreshChronoDisplay()

TRANSITION:
    S: Running.Mode.ChronoMode
    N: Running.Mode.DisplayMode
    E: changeMode
    O: print "ChronoMode -> DisplayMode"
       g.refreshTimeDisplay()

TRANSITION:
    S: Running.Mode.DisplayMode.update
    N: Running.Mode.DisplayMode.update
    T: 1
    O:  g.refreshTimeDisplay()

ENTER:
    N: Running.Light.LightOn
    O: g.setIndiglo()

# time edit mode and timers for selection 

TRANSITION:
    S: Running.Mode.DisplayMode
    N: Running.Mode.DisplayMode.TimeEditWait
    E: editTime

TRANSITION:
    S: Running.Mode.DisplayMode.TimeEditWait
    N: Running.Mode.TimeEdit
    T: 1.5
    O: g.startSelection()

TRANSITION:
    S: Running.Mode.DisplayMode.TimeEditWait
    N: Running.Mode.DisplayMode
    E: released

TRANSITION:
    S: Running.Mode.TimeEdit
    N: Running.Mode.TimeEdit
    E: increase
    O: g.increaseSelection()

TRANSITION:
    S: Running.Mode.TimeEdit
    N: Running.Mode.TimeEdit.FinishedWait.FinishedTimer
    E: finishEdit

TRANSITION:
    S: Running.Mode.TimeEdit.FinishedWait.FinishedTimer
    N: Running.Mode.DisplayMode
    T: 2
    O: g.stopSelection();

TRANSITION:
    S: Running.Mode.TimeEdit.FinishedWait.FinishedTimer
    N: Running.Mode.TimeEdit
    E: released

TRANSITION:
    S: Running.Mode.TimeEdit.TimeEditEndWait
    N: Running.Mode.DisplayMode
    T: 5
    O: g.stopSelection();

TRANSITION:
    S: Running.Mode.TimeEdit.TimeEditEndWait
    N: Running.Mode.TimeEdit.TimeEditEndWait
    E: increase
