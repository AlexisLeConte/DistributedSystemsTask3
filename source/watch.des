IMPORTATION:
  # Import the time
  Time = time.des

INITIALIZER:
    from Tkinter import *
    from DWatchGUI import DWatchGUI
    
    root = Tk()
    root.title("A Title")
    g = DWatchGUI(root, eventhandler)
    test = 1

    eventhandler.start()
    root.mainloop()
    
    exit() # keep SVM from crashing at exit

STATECHART:
    Clock [DS]
    Running [DS]
        TimeUnit [CS][DS] [Time] [IMPORTED=1]
        ChronoTime [CS]
            stop [DS]
            start
                running [DS]

        Light [CS][DS]
            LightOff [DS]
            LightOn
            LightWait

        Mode [CS][DS]
            DisplayMode [DS]
                update [CS][DS]

            ChronoMode
                update [CS][DS]
                Initial [CS][DS]
                Started [CS]
                Stopped

TRANSITION:
    S: Running.Light.LightOff
    N: Running.Light.LightOn
    E: lightOn
    O: print "DisplayMode.LightOff -> DisplayMode.LightOn"

TRANSITION:
    S: Running.Light.LightOn
    N: Running.Light.LightWait
    E: lightOff
    O: print "light on -> light wait"

TRANSITION:
    S: Running.Light.LightWait
    N: Running.Light.LightOff
    T: 2
    O: print "light wait -> light off"
       g.unsetIndiglo()


TRANSITION:
    S: Running.Mode.DisplayMode
    N: Running.Mode.ChronoMode
    E: changeMode
    O: print "DisplayMode -> ChronoMode"
       g.refreshChronoDisplay()

TRANSITION:
    S: Running.Mode.ChronoMode
    N: Running.Mode.DisplayMode
    E: changeMode
    O: print "ChronoMode -> DisplayMode"
       g.refreshTimeDisplay()

TRANSITION:
    S: Running.Mode.DisplayMode.update
    N: Running.Mode.DisplayMode.update
    T: 1
    O:  g.refreshTimeDisplay()


TRANSITION:
    S: Running.Mode.ChronoMode.Initial
    N: Running.Mode.ChronoMode.Started
    E: initChrono
    O: print "init to start"
       test = 0


TRANSITION:
    S: Running.Mode.ChronoMode.Started
    N: Running.Mode.ChronoMode.Stopped
    E: initChrono
    O: print "start to stop"
       test = 1

TRANSITION:
    S: Running.Mode.ChronoMode.update
    N: Running.Mode.ChronoMode.update
    T: 1
    O: g.refreshChronoDisplay()


TRANSITION:
    S: Running.ChronoTime.stop
    N: Running.ChronoTime.start
    C: test == 0
    E: initChrono

TRANSITION:
    S: Running.ChronoTime.start.running
    N: Running.ChronoTime.start.running
    T: 1
    O: g.increaseChronoByOne()

TRANSITION:
    S: Running.ChronoTime.start
    N: Running.ChronoTime.stop
    C: test == 1
    E: initChrono


ENTER:
    N: Running.Light.LightOn
    O: g.setIndiglo()