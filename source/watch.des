INITIALIZER:
    from Tkinter import *
    from DWatchGUI import DWatchGUI
    
    root = Tk()
    root.title("A Title")
    g = DWatchGUI(root, eventhandler)
    
    eventhandler.start()
    root.mainloop()
    
    exit() # keep SVM from crashing at exit

STATECHART:
    System [DS]
        Clock [CS] [DS]
            Ticker [DS]
        Watch [CS] [DS]
            DisplayTime [DS]
            DisplayChrono
        Light [CS] [DS]
            Off [DS]
            On
            Waiting


###############################################################################
# INITIALIZATION                                                              #
###############################################################################

ENTER:
    N: System
    O: SystemTicksCount = 0
       g.resetChrono()

###############################################################################
# SYSTEM CLOCK                                                                #
###############################################################################

TRANSITION:
    S: System.Clock.Ticker
    N: System.Clock.Ticker
    T: 0.25
    O: [EVENT("tick")]
       SystemTicksCount += 1

###############################################################################
# DISPLAY TIME MODE                                                           #
###############################################################################

TRANSITION:
    S: System.Watch.DisplayTime
    N: System.Watch.DisplayTime
    E: tick
    C: SystemTicksCount%4 == 0
    O: g.increaseTimeByOne()
       g.refreshTimeDisplay()

TRANSITION:
    S: System.Watch.DisplayTime
    N: System.Watch.DisplayChrono
    E: changeMode

###############################################################################
# DISPLAY CHRONO MODE                                                         #
###############################################################################

TRANSITION: [0]
    S: System.Watch.DisplayChrono
    N: System.Watch.DisplayChrono
    E: tick
    C: SystemTicksCount%4 == 0
    O: g.increaseTimeByOne()
       g.increaseChronoByOne()
       g.refreshChronoDisplay()

TRANSITION: [1]
    S: System.Watch.DisplayChrono
    N: System.Watch.DisplayChrono
    E: tick
    O: g.increaseChronoByOne()
       g.refreshChronoDisplay()

TRANSITION:
    S: System.Watch.DisplayChrono
    N: System.Watch.DisplayTime
    E: changeMode

###############################################################################
# BACKGROUND LIGHTS                                                           #
###############################################################################

TRANSITION:
    S: System.Light.Off
    N: System.Light.On
    E: lightOn
    O: g.setIndiglo()

TRANSITION:
    S: System.Light.On
    N: System.Light.Waiting
    E: lightOff
    O: LightTicksCount = 0

TRANSITION:
    S: System.Light.Waiting
    N: System.Light.Off
    T: 2
    O: g.unsetIndiglo()

###############################################################################
# DEBUG ENTER/EXIT EVENTS                                                     #
###############################################################################

ENTER:
    N: System.Light.On
    O: print "Enter System.Light.On"

EXIT:
    S: System.Light.On
    O: print "Exit System.Light.On"

ENTER:
    N: System.Light.Off
    O: print "Enter System.Light.Off"

EXIT:
    S: System.Light.Off
    O: print "Exit System.Light.Off"

ENTER:
    N: System.Light.Waiting
    O: print "Enter System.Light.Waiting"

EXIT:
    S: System.Light.Waiting
    O: print "Exit System.Light.Waiting"
